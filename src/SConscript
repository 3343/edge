##
# @file This file is part of EDGE.
#
# @author Alexander Breuer (anbreuer AT ucsd.edu)
#
# @section LICENSE
# Copyright (c) 2019-2020, Alexander Breuer
# Copyright (c) 2015-2018, Regents of the University of California
# All rights reserved.
#
# Redistribution and use in source and binary forms, with or without modification, are permitted provided that the following conditions are met:
#
# 1. Redistributions of source code must retain the above copyright notice, this list of conditions and the following disclaimer.
#
# 2. Redistributions in binary form must reproduce the above copyright notice, this list of conditions and the following disclaimer in the documentation and/or other materials provided with the distribution.
#
# 3. Neither the name of the copyright holder nor the names of its contributors may be used to endorse or promote products derived from this software without specific prior written permission.
#
# THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
#
# @section DESCRIPTION
# Source files for the build.
##

Import('env')

l_vec = []
if  env['compilers'] == 'clang':
  # disable pragma omp simd errors (not vectorized) of sanitizer
  if 'san' in env['mode']:
    l_vec += ['-Wno-error=pass-failed']

l_kernels = [ 'time/TimeGroupStatic.cpp' ]

l_sources = [ 'data/Expression.cpp',
              'data/Dynamic.cpp',
              'data/EntityLayout.cpp',
              'mesh/EdgeV.cpp',
              'dg/Basis.cpp',
              'io/OptionParser.cpp',
              'io/logging.cpp',
              'io/Config.cpp',
              'io/Vtk.cpp',
              'io/WaveField.cpp',
              'io/ErrorNorms.cpp',
              'io/Receivers.cpp',
              'parallel/Shared.cpp',
              'parallel/LoadBalancing.cpp',
              'parallel/Distributed.cpp',
              'parallel/global.cpp',
              'setups/Cpu.cpp',
              'time/Manager.cpp' ]

if 'mpi' in env['parallel']:
  l_sources += [ 'parallel/MpiRemix.cpp' ]

if env['gpi2'] != False:
  l_sources += [ 'parallel/Gaspi.cpp' ]

if 'elastic' in env['equations']:
  l_sources = l_sources + [ 'impl/seismic/io/Config.cpp',
                            'impl/seismic/setups/Elasticity.cpp',
                            'impl/seismic/setups/ViscoElasticity.cpp' ]

for l_source in l_sources:
  env.sources.append( env.Object( l_source ) )

# vector report for kernels
for l_kernel in l_kernels:
  env.sources.append( env.Object( l_kernel, CXXFLAGS = env['CXXFLAGS']+[l_vec] ) )

# gather unit tests
if env['tests']:
  l_tests = ['tests.cpp',
             'data/DataLayout.test.cpp',
             'data/SparseEntities.test.cpp',
             'data/Dynamic.test.cpp',
             'data/Expression.test.cpp',
             'data/MmVanilla.test.cpp',
             'dg/Basis.test.cpp',
             'dg/QuadratureEval.test.cpp',
             'sc/SubGrid.test.cpp',
             'sc/Detections.test.cpp',
             'sc/ibnd/SuperCell.test.cpp',
             'sc/ibnd/Init.test.cpp',
             'monitor/Timer.test.cpp',
             'parallel/Shared.test.cpp',
             'parallel/LoadBalancing.test.cpp',
             'parallel/Distributed.test.cpp',
             'linalg/Geom.test.cpp',
             'linalg/Matrix.test.cpp',
             'linalg/Mappings.test.cpp',
             'linalg/HalfSpace.test.cpp',
             'linalg/Domain.test.cpp',
             'linalg/Series.test.cpp',
             'setups/Cpu.test.cpp',
             'setups/InitialDofs.test.cpp',
             'io/Config.test.cpp',
             'io/Receivers.test.cpp',
             'io/InternalBoundary.test.cpp',
             'impl/swe/solvers/Fwave.test.cpp'
              ]

  if not env['xsmm']:
    l_tests = l_tests + [ 'sc/Kernels.test.cpp' ]

  if env['element_type'] == 'tet4' and env['order'] == '3':
    l_tests = l_tests + ['impl/advection/kernels/TimePred.test.cpp']

  if 'elastic' in env['equations']:
    l_tests = l_tests+['impl/seismic/common.test.cpp',
                       'impl/seismic/sc/Llf.test.cpp',
                       'impl/seismic/setups/Elasticity.test.cpp',
                       'impl/seismic/setups/ViscoElasticity.test.cpp',
                       'impl/seismic/solvers/AderDgInit.test.cpp',
                       'impl/seismic/solvers/FrictionLaws.test.cpp' ]

    # seismic kernel tests are only defined for tets and FP32
    if env['element_type'] == 'tet4' and env['precision'] == '32':
      if env['order'] == '4':
        l_tests += ['impl/seismic/kernels/TimePredVanilla.test.cpp']
        l_tests += ['impl/seismic/kernels/VolIntVanilla.test.cpp']
      if env['order'] == '3':
        l_tests += ['impl/seismic/kernels/SurfIntVanilla.test.cpp']

      if env['xsmm']:
        if env['order'] == '4':
          l_tests += ['impl/seismic/kernels/TimePredSingle.test.cpp']
          l_tests += ['impl/seismic/kernels/VolIntSingle.test.cpp']
        if env['order'] == '3':
          l_tests += ['impl/seismic/kernels/SurfIntSingle.test.cpp']

        if env['cfr'] == '16':
          if env['order'] == '4':
            l_tests += ['impl/seismic/kernels/TimePredFused.test.cpp']
            l_tests += ['impl/seismic/kernels/VolIntFused.test.cpp']
          if env['order'] == '3':
            l_tests += ['impl/seismic/kernels/SurfIntFused.test.cpp']

    if env['hdf5'] != False:
      l_tests = l_tests + ['impl/seismic/setups/PointSources.test.cpp']

  # add objects and make sure we are allowed to overwrite private keywords
  env.tests.append( env.sources )
  l_cxxflags = ['-DPP_UNIT_TEST']
  if env['compilers'] == 'clang':
    l_cxxflags.append( '-Wno-keyword-macro' )
  elif env['compilers'] == 'gnu':
    l_cxxflags.append( '-Wno-type-limits' )

  for l_test in l_tests:
    env.tests.append( env.Object( l_test, CXXFLAGS = env['CXXFLAGS']+l_cxxflags ) )

# prepend main file to edge
env.sources = env.Object( 'main.cpp' ) + env.sources

Export('env')
