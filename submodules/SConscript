##
# @file This file is part of EDGE.
#
# @author Alexander Breuer (anbreuer AT ucsd.edu)
#         Alexander Heinecke (alexander.heinecke AT intel.com)
#
# @section LICENSE
# Copyright (c) 2020, Friedrich Schiller University Jena
# Copyright (c) 2019, Alexander Breuer
# Copyright (c) 2015-2018, Regents of the University of California
# Copyright (c) 2016, Intel Corporation
# All rights reserved.
#
# Redistribution and use in source and binary forms, with or without modification, are permitted provided that the following conditions are met:
#
# 1. Redistributions of source code must retain the above copyright notice, this list of conditions and the following disclaimer.
#
# 2. Redistributions in binary form must reproduce the above copyright notice, this list of conditions and the following disclaimer in the documentation and/or other materials provided with the distribution.
#
# 3. Neither the name of the copyright holder nor the names of its contributors may be used to endorse or promote products derived from this software without specific prior written permission.
#
# THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
#
# @section DESCRIPTION
# Source files in the submodules.
##
import os
import warnings
import subprocess

# get dir of scons script
l_scriptDir = Dir('.').srcnode().abspath

import os
from sys import path

Import('env')
Import('conf')

l_objects = []

# add optionparser
env.Append( CXXFLAGS = [ ('-isystem', 'submodules/optionparser') ] )

# add pugixml
l_objects.append( env.Object( 'pugixml/src/pugixml.cpp',
                               CXXFLAGS = env['CXXFLAGS']+ ['-Wno-error'] ) )
env.Append( CXXFLAGS = [ ('-isystem', 'submodules/pugixml/src') ] )

# add Eigen
env.Append( CXXFLAGS = [ ('-isystem', 'submodules/eigen') ] )
env.Append( CPPDEFINES=['EIGEN_MAX_STATIC_ALIGN_BYTES=0'] )

# enable libdl available
conf.CheckLibWithHeaderFlags('dl', '', 'CXX', [], [], True)

# disable exprtk-optimizations in debug builds
if 'debug' in env['mode']:
  env.Append( CXXFLAGS = ['-Dexprtk_disable_enhanced_features'] )

# forward libxsmm
if env['xsmm'] != False:
  if env['xsmm'] != True:
    env.AppendUnique( CPPPATH=[ env['xsmm']+'/include'] )
    env.AppendUnique( LIBPATH=[ env['xsmm']+'/lib']     )

  if conf.CheckLibWithHeaderFlags( 'libxsmmnoblas', 'libxsmm.h' ) and \
     conf.CheckLibWithHeaderFlags( 'libxsmm', 'libxsmm.h' ):
    env.AppendUnique( LINKFLAGS=['-pthread'] )
    if env['cfr'] == '1':
      env.AppendUnique( CPPDEFINES=['PP_T_KERNELS_XSMM_DENSE_SINGLE'] )
    else:
      env.AppendUnique( CPPDEFINES=['PP_T_KERNELS_XSMM'] )
    if env['precision'] == '32':
      if env['arch'] in ['hsw', 'skx', 'avx512']:
        env.AppendUnique( CPPDEFINES=['PP_T_KERNELS_XSMM_ELTWISE_TPP'] )
    if env['precision'] == '64':
      if env['arch'] in ['hsw', 'skx', 'avx512']:
        env.AppendUnique( CPPDEFINES=['PP_T_KERNELS_XSMM_ELTWISE_TPP'] )
  else:
    warnings.warn('  Warning: Could not enable libxsmm, continuing without.' )
    env.AppendUnique( CPPDEFINES=['PP_T_KERNELS_VANILLA'] )
    env['xsmm'] = False
else:
  env.AppendUnique( CPPDEFINES=['PP_T_KERNELS_VANILLA'] )

# enable zlib if available
if env['zlib'] != False:
  if env['zlib'] != True:
    env.AppendUnique( CPPPATH=[ env['zlib']+'/include'] )
    env.AppendUnique( LIBPATH=[ env['zlib']+'/lib']     )
  if not conf.CheckLibWithHeaderFlags( 'z', 'zlib.h', 'CXX' ):
    warnings.warn( '  Warning: Could not enable zlib.' )

# enable HDF5 if available
if env['hdf5'] != False:
  if env['hdf5'] != True:
    env.AppendUnique( CPPPATH=[ env['hdf5']+'/include'] )
    env.AppendUnique( LIBPATH=[ env['hdf5']+'/lib']     )
  if conf.CheckLibWithHeaderFlags( 'hdf5' ) and conf.CheckLibWithHeaderFlags( 'hdf5_hl' ):
    env.AppendUnique( CPPDEFINES = ['PP_HAS_HDF5'] )
  else:
    warnings.warn( '  Warning: Could not enable HDF5.' )

# enable GPI2 if available
if env['gpi2'] != False:
  if env['gpi2'] != True:
    env.AppendUnique( CPPPATH=[ env['gpi2']+'/include'] )
    env.AppendUnique( LIBPATH=[ env['gpi2']+'/lib']     )
    env.AppendUnique( LIBPATH=[ env['gpi2']+'/lib64']   )
  if conf.CheckLibWithHeaderFlags( 'GPI2' ):
    env.AppendUnique( CPPDEFINES = ['PP_USE_GASPI'] )
  # enable ibverbs if available
  conf.CheckLibWithHeaderFlags( 'ibverbs', '', 'CXX', [], [], True)


# forward Gmsh
if env['gmsh'] != False:
  if env['gmsh'] != True:
    env.PrependUnique( LIBPATH=[ env['gmsh']+'/lib']     )
    env.PrependUnique( LIBPATH=[ env['gmsh']+'/lib64']   )
  conf.CheckLibWithHeaderFlags( 'gmp', '', i_dynamic=True )
  conf.CheckLib( 'gmsh', language='CXX' )

# forward EDGE-V
if env['edge_v'] != False:
  if env['edge_v'] != True:
    env.AppendUnique( CPPPATH=[ env['edge_v']+'/include'] )
    env.AppendUnique( CPPPATH=[ env['edge_v']+'/include/third'] )
    env.AppendUnique( LIBPATH=[ env['edge_v']+'/lib'] )

  if not conf.CheckLibWithHeaderFlags( 'edge_v', 'edge_v/edge_v.h' ):
    warnings.warn( '  Warning: Could not enable EDGE-V.' )

# create visit writer separately to get around "format string" warnings
env.sources.append( env.Object( 'visit_writer/visit_writer.cpp',
                                CXXFLAGS = env['CXXFLAGS']+
                                           ['-Wno-error']+
                                           ['-Wno-shadow']+
                                           ['-Wno-unused-variable']+
                                           ['-Wno-format-security'] ) )

# create easylogging separately to get around shadow warnings
if env['easylogging'] != False:
  env.AppendUnique( CPPDEFINES=['PP_USE_EASYLOGGING'] )
  l_el = {}
  l_el['def'] = ['ELPP_NO_DEFAULT_LOG_FILE']
  if 'omp' in env['parallel']:
    l_el['def']=l_el['def']+['ELPP_THREAD_SAFE']
  l_el['obj'] = env.Object( 'easylogging/src/easylogging++.cc',
                            CXXFLAGS   = env['CXXFLAGS']+
                                        ['-Wno-shadow',
                                         '-Wno-range-loop-construct'],
                            CPPDEFINES = l_el['def'] )
  l_easyLogging = env.StaticLibrary( 'easylogging/easylogging',
                                     l_el['obj'] )
  env.Append( LIBS = l_easyLogging )
  env.Append( CXXFLAGS = [ ('-isystem', 'submodules/easylogging/src') ] )

# compile numerical libs separately
l_numLibs = [ 'FastGL/fastgl.cpp' ]
for l_lib in l_numLibs:
  env.sources.append( env.Object( l_lib,
                                  CXXFLAGS = env['CXXFLAGS']+
                                             ['-Wno-error']+
                                             ['-Wno-all']+
                                             ['-Wno-shadow']+
                                             ['-Wno-unused-variable'] ) )

# compile default sources
for l_object in l_objects:
  env.sources.append( l_object )

# add EDGE's pre-processed data
def csvToCpp( target, source, env ):
  assert( len(target) == 1 )
  assert( len(source) == 1 )

  # assemble args
  l_in  = source[0].get_abspath()
  l_ons = env['edge_pre'][source[0].get_abspath()]['ons']
  l_on  = env['edge_pre'][source[0].get_abspath()]['on']
  l_ot  = env['edge_pre'][source[0].get_abspath()]['ot']
  l_out = target[0].get_abspath()

  l_comm = [ 'tools/edge_pre/cpp.py',
             '-i', l_in,
             '-s', l_ons,
             '-n', l_on,
             '-t', l_ot,
             '-o', l_out ]
  if 'r' in env['edge_pre'][source[0].get_abspath()]:
    l_comm = l_comm + ['-r'] + env['edge_pre'][source[0].get_abspath()]['r']
  l_proc = subprocess.Popen( l_comm, stdout=subprocess.PIPE )
  l_pOut, l_pErr = l_proc.communicate()
  print( l_pOut )

edgePre = {}
edgePre['src'] = []
env['edge_pre'] = {}

# EDGEpre's DG data
l_dgPre =  ['mass', 'stiffT', 'stiffV', 'fluxL', 'fluxN', 'fluxT']
# EDGEpre's SC data
l_scPre = ['svcrds', 'scsv', 'scsfsc', 'sctysf', 'scdgad', 'gather', 'scatter', 'scattersurf', 'sfint']

# add EDGEpre's data
for l_pre in l_dgPre+l_scPre:
  # set type
  if l_pre in l_dgPre:
    l_ty = 'dg'
  else:
    l_ty = 'sc'

  # generate file names
  l_dir = 'edge_pre/' + l_ty + '/' + env['element_type'] + '/' + str( int(env['order'])-1 ) + '/'
  l_base = l_dir + env['element_type'] +\
             '_' +str( int(env['order'])-1 ) + '_' + l_pre
  l_csv = File( l_scriptDir + '/' + l_base + '.csv' )
  l_cpp = File( l_base + '.cpp' )

  # set info
  env['edge_pre'][l_csv.get_abspath()] = {}
  env['edge_pre'][l_csv.get_abspath()]['ons'] = l_ty
  env['edge_pre'][l_csv.get_abspath()]['on']  = l_pre

  if l_pre in ['svcrds', 'gather', 'scatter', 'scattersurf', 'sfint']+l_dgPre:
    env['edge_pre'][l_csv.get_abspath()]['ot']  = 'double'
  elif l_pre in ['scsv', 'scsfsc', 'sctysf', 'scdgad']:
    env['edge_pre'][l_csv.get_abspath()]['ot']  = 'unsigned short'
    env['edge_pre'][l_csv.get_abspath()]['r']  = ['-1', 'std::numeric_limits< unsigned short >::max()']
  else: assert( False )

  # create command to build file
  env.Command( l_cpp, l_csv, csvToCpp )

  # append object to edge_pre library
  edgePre['src'].append( env.Object( l_cpp ) )

# create a library out of EDGE's pre-processed data
edgePre['lib'] = env.StaticLibrary( 'edge_pre/edge_pre', edgePre['src'] )

# attach library to build
env.Append( LIBS = edgePre['lib'] )

Export('conf')
Export('env')
